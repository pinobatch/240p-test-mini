;
; Motion blur test for 240p test suite
; Copyright 2018 Damian Yerrick
;
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License along
; with this program; if not, write to the Free Software Foundation, Inc.,
; 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
;

include "src/gb.inc"
include "src/global.inc"

; This motion blur test also replaces 100 IRE.

  rsset hTestState
def back_shade   rb 1  ; 0-3 or 0-31
def on_time      rb 1  ; 1-20
def on_shade     rb 1  ; 0-3 or 0-31
def off_time     rb 1  ; 1-20
def off_shade    rb 1  ; 0-3 or 0-31
def is_stripes   rb 1  ; 0: colors 2 and 3 both on or off; 1: one on, other off
def is_running   rb 1  ; $00: off; $80: in off time; $80: in on time
def frames_left  rb 1  ; until toggle bit 0
def bgp_value    rb 8

section "motionblur_map0",ROM0
wndtxt:
  db   0,"Back shade",10
  db  48,"time",10
  db  64,"Stripes",10
  db  98,"Off",10
  db 114,"On",0

wndmap:
  db $20,$21,$22,$23,$24,$25  ; Back shade
  db $2E,$2F,$00,$26,$27,$00  ; On time
  db $00,$00,$00,$23,$24,$25  ;    shade
  db $2C,$2D,$00,$26,$27,$00  ; Off time
  db $00,$00,$00,$23,$24,$25  ;     shade
  db $28,$29,$2A,$2B,$00,$00  ; Stripes

def MONO_MAX_GRAY equ 3
def COLOR_MAX_GRAY equ 31
def MAX_PHASE_DURATION equ 60

row_high_low:  ; low and high-low+1 values for each row, monochrome
  db 0, MONO_MAX_GRAY+1
  db 1, MAX_PHASE_DURATION
  db 0, MONO_MAX_GRAY+1
  db 1, MAX_PHASE_DURATION
  db 0, MONO_MAX_GRAY+1
  db 0, 2

row_high_low_gbc:  ; low and high-low+1 values for each row, SGB/GBC
  db 0, COLOR_MAX_GRAY+1
  db 1, MAX_PHASE_DURATION
  db 0, COLOR_MAX_GRAY+1
  db 1, MAX_PHASE_DURATION
  db 0, COLOR_MAX_GRAY+1
  db 0, 2

section "motionblur",ROM0

activity_motion_blur::
  call flashing_consent
  ret z

  xor a
  ldh [frames_left],a  ; = 0
  ldh [is_running],a
  ldh [is_stripes],a
  ldh [off_shade],a
  ld [help_cursor_y],a
  inc a
  ldh [off_time],a     ; = 1
  ldh [on_time],a

  ; Calculate starting on and back shades: 3 and 1 for DMG,
  ; 31 and 15 for GBC or SGB
  ldh a, [hw_capability]
  cp 1     ; CF = 1 for DMG or 0 for SGB/GBC
  sbc a    ; A = $FF for DMG or 0 for SGB/GBC
  and COLOR_MAX_GRAY^MONO_MAX_GRAY  ; A = diff for DMG or 0 for SGB/GBC
  xor COLOR_MAX_GRAY
  ldh [on_shade],a
  rra
  ldh [back_shade],a
.restart:
  call clear_gbc_attr
  call clear_sgb_attr  ; because we use PAL01
  call load_common_obj_tiles

  ; Clear tilemaps to color 0
  call clear_scrn0_to_0

  ; Load label tiles
  call vwfClearBuf
  ld hl,wndtxt
  .labelloadloop:
    ld a,[hl+]
    ld b,a
    call vwfPuts
    ld a,[hl+]
    or a
    jr nz,.labelloadloop
  ld hl,CHRRAM0+$20*16
  ld bc,$0010  ; planes 0 and 1, c=16 tiles
  call vwfPutBuf03

  ; Load label map
  ld bc,$0606  ; size
  ld hl,wndmap
  ldxy de, 6, 11
  call load_nam

  ; Draw 8x8-tile rect of solid $0A (striped tile) starting at (6, 2)
  ldxy hl, 6, 2
  ld a,$0A
  ld b,8
  ld de,32-8
  .rectrowloop:
    ld c,8
    rst memset_tiny
    add hl,de
    dec b
    jr nz,.rectrowloop

  ; Turn on rendering (no sprites)
  ld a,%10101010
  call set_bgp  ; Palette all gray until calculated
  ld a,LCDCF_ON|BG_NT0|BG_CHR01
  ldh [vblank_lcdc_value],a
  ldh [rLCDC],a
  call motionblur_update_sgb

.loop:
  ld b,helpsect_motion_blur
  call read_pad_help_check
  jr nz,.restart

  ; Process input
  ldh a,[new_keys]
  bit PADB_B,a
  ret nz
  call motionblur_handle_presses
  call make_help_vram
  call motionblur_calc_bgp
  rst wait_vblank_irq

  ; Set mono palette
  ld hl, bgp_value
  ld a, [hl]
  ldh [rBGP],a

  ; Set GBC palette if on GBC
  ld bc,$0800|low(rBCPS)
  ldh a, [hw_capability]
  and $80
  call nz, set_gbc_palette

  ; Blit help map
  ld bc,$0206  ; size
  ld hl,help_line_buffer
  ldxy de, 12, 11
  call load_nam

  ; Draw cursor
  ldxy hl, 5, 11
  ld a,[help_cursor_y]
  inc a
  ld c,a
  ld b,6
  ld de,32
  .draw_cursor_loop:
    xor a
    dec c
    jr nz,.draw_cursor_not_this_row
      ld a,$05
    .draw_cursor_not_this_row:
    ld [hl],a
    add hl,de
    dec b
    jr nz,.draw_cursor_loop

  jr .loop

;;
; Updates help_cursor_y and the variable pointed to.
; @param DE pointer to a list of (lowest, range size)
; @return A=current value, D=previous value, B=pressed keys
motionblur_handle_dpad::
  ldh a,[new_keys]
  ld b,a

  ; Up/Down: Choose a row
  ld a,[help_cursor_y]
  bit PADB_UP,b
  jr z,.no_cursor_up
    dec a
  .no_cursor_up:
  bit PADB_DOWN,b
  jr z,.no_cursor_down
    inc a
  .no_cursor_down:

  ; Write back only if within range
  cp 6
  jr nc,.no_writeback_cursor_y
    ld [help_cursor_y],a
  .no_writeback_cursor_y:

  ; Fetch value and lower/upper bounds for this row
  ld a,[help_cursor_y]
  add low(back_shade)
  ld c,a  ; C points to HRAM for row B

  ; Ranges differ between mono and GBC
  ld a,[help_cursor_y]
  call de_index_a  ; Valid values are L <= x < H
  ldh a, [c]

  ; Left/Right: Change value on this row.  D = old value
  ld d, a
  bit PADB_LEFT,b
  jr z,.no_cursor_left
    dec a
  .no_cursor_left:
  bit PADB_RIGHT,b
  jr z,.no_cursor_right
    inc a
  .no_cursor_right:
  
  ; Write back only if within range
  sub l
  cp h
  ret nc
  add l
  ldh [c],a
  ret


motionblur_handle_presses:
  ld b,PADF_UP|PADF_DOWN|PADF_LEFT|PADF_RIGHT
  call autorepeat_60hz_if_sgb

  ; Load the set of high and low water marks for DMG vs. others
  ld de,row_high_low
  ldh a, [hw_capability]
  or a
  jr z,.not_gbc_ranges
    ld de,row_high_low_gbc
  .not_gbc_ranges:

  ; If flicker not running, update graphics
  ldh a,[is_running]
  rla
  jr c, .skip_dpad
    call motionblur_handle_dpad
    cp d
    call nz, motionblur_update_sgb
  .skip_dpad:

  ldh a, [new_keys]
  and PADF_A
  ret z

  ; When A is pressed, set frames_left to 1 so that the animation
  ; restarts immediately.
  ; ld a, 1  ; frames_left is 1, but so is PADF_A
  ldh [frames_left],a

  ; Toggle running flag and clear its phase
  ldh a,[is_running]
  cpl
  and $80
  ldh [is_running],a
  ret

make_help_vram:
  xor a
  ld bc,5*256+low(back_shade)
  ld hl,help_line_buffer
  .loop:
    ldh a, [c]
    push bc
    call bcd8bit_ac
    jr nz,.has_tens
      ld a,$10
    .has_tens:
    xor $10
    ld [hl+],a
    ld a,c
    xor $10
    ld [hl+],a
    pop bc
    inc c
    dec b
    jr nz,.loop
  ldh a,[is_stripes]
  add a
  add $2C  ; On/Off text
  ld [hl+],a
  inc a
  ld [hl],a
  ret

motionblur_calc_bgp:
  ; If running, clock the timer forward one frame
  ldh a,[is_running]
  ld b,a
  add a
  jr nc,.not_running
    ldh a,[frames_left]
    dec a
    ldh [frames_left],a
    jr nz,.not_running

    ; Get the remaining time for the new frame.  On is 1 in
    ; is_running but first in the menu, so do this before
    ; toggling the frame bit.
    ld a,b
    add a
    add low(on_time)
    ld c,a
    ldh a, [c]
    ldh [frames_left],a

    ; And toggle to the other frame
    ld a,b
    xor $01
    ldh [is_running],a
  .not_running:

  ; Fetch the on and off shades
  ldh a,[off_shade]
  ld e,a
  ldh a,[on_shade]
  ld d,a
  ldh a,[back_shade]
  ld b,a
  ldh a, [hw_capability]
  rra
  jr nc, .not_sgb
    ld b, $00
    ld de, $0203
  .not_sgb:
  ld c,b
  ; Now b=c=back shade, d=on shade, e=off shade

  ; If current step (bit 0) is off, swap the two
  ldh a,[is_running]
  rra
  jr c,.no_swap
    ld a,d
    ld d,e
    ld e,a
  .no_swap:
  ; b=c=back shade, d=current frame shade, e=other frame shade

  ; If stripes are off, use the current frame shade for both rows
  ldh a,[is_stripes]
  rra
  jr c,.no_destripe
    ld e,d
  .no_destripe:
  ; b=c=back shade, d=even rows shade, e=odd rows shade

  ldh a,[hw_capability]
  add a
  jr c,.is_gbc

    ; If not running, use the front shade with the maximum contrast
    ldh a,[is_running]
    add a
    jr c,.mono_have_text_shade
      ld c, 1
      ldh a, [hw_capability]
      rra
      jr c, .mono_have_text_shade   ; SGB: use 1
      dec c
      bit 1, b
      jr nz, .mono_have_text_shade  ; 2 or 3: use 0
      ld c, 3                       ; Otherwise use 3
    .mono_have_text_shade:
    ; b=back shade, c=front shade,
    ; d=even rows shade, e=odd rows shade

    ; Combine the four shades
    swap d
    swap e
    ld a,c
    or e  ; A = odd rows shade << 4 | front shade
    add a
    add a  ; A = odd rows shade << 6 | front shade << 2
    or b
    or d
    ; A = odd rows shade << 6 | even rows shade << 4
    ;     | front shade << 2 | back shade
    ldh [bgp_value],a
    ret
  .is_gbc:

  ; If not running, use the front shade with the maximum contrast
  ldh a,[is_running]
  add a
  jr c,.is_running_gbc
  .sgb_calc:
    ld c,0
    bit 4,b
    jr nz,.is_running_gbc
    ld c,31
  .is_running_gbc:
  ; b=back shade, c=front shade,
  ; d=even rows shade, e=odd rows shade

  push de
  ld e,c
  ld d,b
  ld c,low(bgp_value)
  call .twoshades
  pop de
.twoshades:
  push de
  ld a,d
  call .oneshade
  pop de
  ld a,e
.oneshade:
  ld e,a
  add a
  add a
  ld d,a     ; DE = $0401 * gray
  add a
  ld l,a
  ld h,0     ; HL = $0008 * gray
  add hl,hl
  add hl,hl  ; HL = $0020 * gray
  add hl,de
  ; write to 
  ld a,l
  ldh [c],a
  inc c
  ld a,h
  ldh [c],a
  inc c
  ret

motionblur_update_sgb:
  ldh a,[off_shade]
  ld e,a
  ldh a,[on_shade]
  ld d,a
  ldh a,[back_shade]
  ld b,a
  call motionblur_calc_bgp.sgb_calc
  ld de, help_line_buffer
  push de
  ld a, $00*8+1
  ld [de], a
  inc de
  ld bc, 8
  ld hl, bgp_value
  call memcpy
  ld c, 7
  ld h, 0
  call memset
  pop hl
  jp sgb_send_if_sgb
