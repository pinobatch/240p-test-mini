;
; Init code for Game Boy
;
; Copyright 2018 Damian Yerrick
; 
; This software is provided 'as-is', without any express or implied
; warranty.  In no event will the authors be held liable for any damages
; arising from the use of this software.
; 
; Permission is granted to anyone to use this software for any purpose,
; including commercial applications, and to alter it and redistribute it
; freely, subject to the following restrictions:
; 
; 1. The origin of this software must not be misrepresented; you must not
;    claim that you wrote the original software. If you use this software
;    in a product, an acknowledgment in the product documentation would be
;    appreciated but is not required.
; 2. Altered source versions must be plainly marked as such, and must not be
;    misrepresented as being the original software.
; 3. This notice may not be removed or altered from any source distribution.
;
include "src/gb.inc"
include "src/global.inc"

; It is not strictly necessary to completely clear the display list,
; as lcd_clear_oam writes a "hide this sprite" value to all unused
; sprites.  But when Options > Exceptions > Unitialized RAM >
; break on read (reset) is on, BGB complains about copying
; uninitialized data to OAM even if said data is provably unused.
; So put "shut BGB up" commands behind a conditional.
def bgbcompat equ 1

; Like the NES and Super NES, DMA on the Game Boy takes access to RAM
; and ROM away from the CPU.  But unlike those two, the CPU continues
; to run.  So store a tiny subroutine in HRAM (tightly coupled
; memory) to keep the CPU busy until OAM DMA is done.
section "hram_init", HRAM[$FFF4]
run_dma:: ds 11

; For the purpose of hLocals, see a fairly long rant in global.inc
section "hram_teststate", HRAM[hTestState]
  ds test_state_size

def STACK_SIZE EQU 64
section "stack", WRAM0, ALIGN[2]
stack_top: ds STACK_SIZE

section "bootregs_hram", HRAM
hw_capability:: ds 1

section "bootregs", WRAM0
initial_a:: ds 1
initial_b:: ds 1

section "rom_init", ROM0
reset_handler::
  di  ; Disable interrupts
  ld sp, stack_top + STACK_SIZE  ; Set up stack pointer (full descending)

  ; A and B used for DMG, SGB, GB Pocket, GBC, or GBA detection.
  ; Useful for motion blur compensation, palette adjustment,
  ; music adjustment on original SGB, etc.
  ; A=01: DMG or SGB
  ; A=FF: Game Boy Pocket or SGB2
  ; A=11, B.0=0: GBC
  ; A=11, B.0=1: GBA
  ; We'll be translating that to a single hardware capability value:
  ; $00 for mono handheld, $01 for Super Game Boy,
  ; $80 for GBC, or $C0 for GBA.
  ; But we also save initial A and B values for machine type
  ; diagnostic display in "Lame boy".
  ld hl, initial_a
  ld [hl+], a
  ld [hl], b
  xor $11
  cp 1  ; Carry true if GBC or GBA, false for mono

  ; While carry is set for GBC or clear otherwise, initialize
  ; controller reading now because SGB detection uses it.
  ; Must preserve carry across this whole block.
  ld a,P1F_GET_NONE
  ldh [rP1],a
  ld a, 1
  ld [rROMB0], a  ; bug #61: some buggy flash carts don't init ROM bank to 1
  dec a
  ldh [cur_keys],a
  ld hl, das_keys
  ld [hl+], a  ; clear das_keys
  ld [hl+], a  ; clear das_timer

  jr nc, .not_gba_check
    rr b
    ld a, $03  ; GBC: 0|03  GBA: 1|03
    rra        ; GBC: 1|01  GBA: 1|81
    rra        ; GBC: 1|80  GBA: 1|C0
    ; and carry is still set to skip the fading
  .not_gba_check:
  ldh [hw_capability], a

  ; To distinguish the SGB models from GB, the game's header must
  ; declare SGB awareness ($0146=$03 and $014B=$33).  Then the game
  ; should send a MLT_REQ command to go to 2-player mode, call
  ; read_pad a few times, and look at bits 1-0 of rP1 after releasing
  ; the key matrix.  These bits are 3 on handhelds; on SGB, they
  ; indicate which controller will be polled by the NEXT call to
  ; read_pad: 3 means player 1, 2 means player 2, and 1 and 0 mean
  ; multitap players 3 and 4.

  ; Because SGB detection takes a few frames, fade out the IPL logo
  ; while it's happening.  Skip it on GBC for two reasons:
  ; GBC+SGB is not authentic hardware, and GBC IPL already faded
  ; out its logo.  Carry is still set on GBC.
  ld b, 1  ; Wait for vblank only once if on GBC
  jr c,.no_fade

  ld a,%00001000  ; bits 3-2: dark gray
  ldh [rBGP],a
  ; Super Game Boy appears not to listen for packets for the
  ; first few frames after starting the Game Boy.  So do the
  ; detection on the light gray frame of the fadeout instead.
  call sgb_wait
  call sgb_wait

  ld a,%00000100  ; bits 3-2: light gray
  ldh [rBGP],a
  call detect_sgb

  xor a
  ldh [rBGP],a    ; white palette
  ldh [rSCY], a   ; clear scroll
  ldh [rSCX], a   ; clear scroll
  ldh [rNR52], a  ; disable (and reset) audio

  ; Rendering can be turned off only during blanking.  Fortunately
  ; the fadeout loop left us in vblank.  So set up the video
  ; hardware.
.no_fade:
  call lcd_off

  if bgbcompat
    ; BGB triggers spurious "reading uninitialized WRAM" exceptions
    ; when DMAing the X, tile number, and attribute of an OAM entry
    ; whose Y is offscreen.
    ld hl,SOAM
    ld c,160
    rst memset_tiny

    ; The VWF engine always modifies the tile overlapping the right
    ; half of a glyph, even if the glyph's opaque area doesn't extend
    ; into that tile.  Occasionally, it may extend one tile past the
    ; 16 tiles (128 pixels, 128 bytes) actually available in the
    ; buffer.  For this reason, I have reserved at least one
    ; additional tile that's never copied to VRAM so that when
    ; drawing overflows, it at least doesn't hurt anything.
    ; But when VWF drawing overflows into this tile, BGB triggers
    ; spurious "reading unitialized WRAM" exceptions.
    ld hl,lineImgBuf
    ld c,136  ; used size plus 8 for the spare tile
    rst memset_tiny
  endc

  ld a, $80
  ldh [vblank_lcdc_value], a
  ; Copy the sprite DMA routine to HRAM
  ld hl,hramcode_start
  ld de,run_dma
  call memcpy_pascal16
  jp main

section "memcpy", ROM0
clear_scrn0_to_0::
  ld h, 0
  fallthrough clear_scrn0_to_h
clear_scrn0_to_h::
  ld de,_SCRN0
  ld bc,32*32
  fallthrough memset

;;
; Writes BC bytes of value H starting at DE.
memset::
  ; Increment B if C is nonzero
  dec bc
  inc b
  inc c
  ld a, h
.loop:
  ld [de],a
  inc de
  dec c
  jr nz,.loop
  dec b
  jr nz,.loop
  ret

;;
; Copy a string preceded by a 2-byte length from HL to DE.
; @param HL source address
; @param DE destination address
memcpy_pascal16::
  ld a, [hl+]
  ld c, a
  ld a, [hl+]
  ld b, a
  fallthrough memcpy

;;
; Copies BC bytes from HL to DE.
; @return A: last byte copied; HL at end of source;
; DE at end of destination; B=C=0
memcpy::
  ; Increment B if C is nonzero
  dec bc
  inc b
  inc c
.loop:
  ld a, [hl+]
  ld [de],a
  inc de
  dec c
  jr nz,.loop
  dec b
  jr nz,.loop
  ret

;;
; The routine gets copied to high RAM.  While OAM DMA is running,
; both ROM and WRAM are inaccessible; only HRAM is readable.
; But unlike on the NES, the CPU continues to fetch and execute
; instructions.  So a program needs to run 160 mcycles' worth of
; code from HRAM until this finishes.  Thus to present a display
; list, the program will call run_dma, not hramcode_start.
hramcode_start:
  dw hramcode_end-hramcode_start-2
  ld a,SOAM >> 8
  ldh [rDMA],a
  ld a,40
.loop:
  dec a
  jr nz,.loop
  ret
hramcode_end:

; If we stuff these in their own sections, RGBLINK might be able to
; pack them between the unused IRQ/RST vectors, at least until we
; figure out which to make into RSTs.


section "memset_tiny",ROM0[$08]
;;
; Writes C bytes of value A starting at HL.
memset_tiny::
  ld [hl+],a
  dec c
  jr nz,memset_tiny
  ret

section "memset_inc",ROM0
;;
; Writes C bytes of value A, A+1, ..., A+C-1 starting at HL.
memset_inc::
  ld [hl+],a
  inc a
  dec c
  jr nz,memset_inc
  ret
