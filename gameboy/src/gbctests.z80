;
; Game Boy Color-exclusive tests for 240p test suite
; Copyright 2018 Damian Yerrick
;
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License along
; with this program; if not, write to the Free Software Foundation, Inc.,
; 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
;
include "src/gb.inc"
include "src/global.inc"

  rsset hTestState
def curhelpsect rb 1
def curpalette rb 1
def curvolume rb 1
def cur_lcdc rb 1
def need_sgb_update rb 1

; Game Boy Color tests menu ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
section "gbctestmenu_about", ROM0
activity_about_gbc_tests:
  ld b,helpsect_color_tests
  jp helpscreen_abslr

;;
; An additional menu of tests for SGB or GBC
activity_gbc_tests_menu::
  ; First ensure that the machine is SGB or GBC.
  ldh a,[hw_capability]
  or a
  jr z, activity_about_gbc_tests

.loop:
  ldh a, [hw_capability]
  and $01  ; A=1 for SGB or 0 for GBC
  add helpsect_color_tests_menu  ; GBC and SGB must be consecutive
  ld b, a
  ld a,PADF_A|PADF_B|PADF_START|PADF_DOWN|PADF_UP|PADF_LEFT|PADF_RIGHT
  call helpscreen

  ; B to return to the main menu
  ldh a, [new_keys]
  ld d, a
  and PADF_B
  ret nz

  ; Save menu selection and get the index into the handler list
  ld hl, help_cursor_y
  ld a,[hl+]
  ld b,a
  ld c,[hl]
  push bc

  ; Start does About instead of what is selected
  bit PADB_START, d
  jr z,.not_start
    call activity_about_gbc_tests
    jr .skip_activity
  .not_start:

  ; Get the handler for this activity
  ld de,color_handlers
  ldh a, [hw_capability]
  rra
  sbc a  ; A = $FF for SGB, $00 for GBC
  and (super_handlers - color_handlers) / 2
  add b  ; add cursor Y saved above
  call jumptable_de_a
  calls color_handlers
  calls super_handlers
.skip_activity:

  ; Restore menu selection
  pop bc
  ld hl, help_cursor_y
  ld a, b
  ld [hl+], a
  ld [hl], c
  jr .loop

color_handlers:
  jumptable
  dw activity_pluge
  dw activity_gradient_bars
  dw activity_smpte_bars
  dw activity_barsongray
  dw activity_color_bleed
super_handlers:
  jumptable
  dw activity_pluge
  dw activity_barsongray
  dw activity_color_bleed
  dw activity_sgb_ntsc
  dw activity_sgb_sound

; PLUGE ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "pluge", ROM0
pluge_sgb_rects:
  db $04*8+1  ; 1 packet holds up to 2 rectangles
  db 2        ; number of rectangles

  db %00000111  ; bit 0: inside, 1: border, 2: outside
  db %00001010  ; inside and border to palette 2, outside to palette 0
  db 10, 0, 19, 17  ; right half of screen

  db %00000011
  db %00001111  ; inside and border to palette 3
  db 10, 13, 19, 17  ; bottom right

  db 0, 0

activity_pluge:
  xor a
  ldh [curpalette],a
  ldh [curvolume],a  ; 1: shark; 0: pluge
.restart:
  call clear_gbc_attr

  ; Load PLUGE tiles
  call load_common_obj_tiles
  ld hl,CHRRAM0+4*16+1
  ld b,8
  ld a,$FF
  .remap8:
    ld [hl+],a
    inc l
    dec b
    jr nz,.remap8

  ; Load shark tiles
  ld hl,CHRRAM0+32*16
  ld de,pluge_shark_chr
  ld b,16
  call pb16_unpack_block

  ; Load main map
  ld de,pluge_rowmap
  ldh a, [hw_capability]
  rra
  jr nc,.not_sgb_alt_map
    ld hl, pluge_sgb_rects
    call sgb_send
    ld de, pluge_sgb_rowmap
  .not_sgb_alt_map:
  call smpte_load_map_SCRN0
  ld hl,pluge_palette_common
  ld bc,8*256+low(rBCPS)
  ld a,$88
  call set_gbc_palette

  ; Load shark map (all palette 0)
  ld hl,_SCRN1
  ld b,18
  ld a,$20
  .sharkmaprowloop:
    ld c,32
    .sharkmaptileloop:
      ld [hl+],a
      add 4
      and $2F
      dec c
      jr nz,.sharkmaptileloop
    inc a
    and $23
    dec b
    jr nz,.sharkmaprowloop

  ld a,%11100100
  ldh [rBGP], a
  ld a,LCDCF_ON|BG_NT0|BG_CHR01
  ldh [vblank_lcdc_value],a
  ldh [rLCDC],a
  ldh a, [hw_capability]
  ldh [need_sgb_update], a

.loop:
  ld b,helpsect_pluge
  call read_pad_help_check
  jr nz,.restart

  ldh a,[new_keys]
  ld b,a
  bit PADB_B,b  ; B: exit
  ret nz

  bit PADB_A,b  ; A: toggle palette
  jr z,.notogglepalette
    ld c,2
    ldh a,[curvolume]
    or a
    jr z,.only_two_palettes
      ld c,5
    .only_two_palettes:
    ldh a,[curpalette]
    inc a
    cp c
    jr c,.no_palette_wraparound
      xor a
    .no_palette_wraparound:
    ldh [curpalette],a
    jr .schedule_sgb_update
  .notogglepalette:

  bit PADB_SELECT,b  ; Select: toggle background type
  jr z,.notogglebgtype
    ldh a,[curvolume]
    xor 1
    ldh [curvolume],a
    xor a
    ldh [curpalette],a
  .schedule_sgb_update:
    ldh a, [hw_capability]
    ldh [need_sgb_update], a
  .notogglebgtype:
  ldh a,[curvolume]
  add a
  add a
  add a  ; bit 3: SCRN0 or SCRN1
  or LCDCF_ON|BG_NT0|BG_CHR01
  ldh [vblank_lcdc_value],a

  rst wait_vblank_irq

  ; Load palette and tiles
  ld de,pluge_palette_ntsc
  ldh a,[curvolume]
  or a
  jr z,.not_load_shark_palette
    ld de,pluge_shark_palette
  .not_load_shark_palette:
  ldh a,[curpalette]

  ; DE: which palette set to use; A: which entry in the set
  ld h,0
  add a
  add a
  add a
  ld l,a
  add hl,de
  ld a,$80
  ld bc,(1*8)*256+low(rBCPS)
  call set_gbc_palette

  ldh a, [need_sgb_update]
  rra
  jr nc, .no_sgb_update
    ldh a, [curpalette]
    add $1C
    ld b, a
    ld c, a
    ld d, a
    ld e, a
    xor a
    ldh [need_sgb_update], a

    ldh a, [curvolume]
    or a
    jr nz,.sgb_not_shark_palette
      ld a, b
      add $28-$1C
      ld b, a
      ld de, $2627
    .sgb_not_shark_palette:
    call sgb_set_palettes_bcde
  .no_sgb_update:

  jr .loop

; Gradient color bars ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "gradientbars", ROM0
; The left three steps of the gradient are drawn with sprites
; because the background palette is full
gradient_objpalette:
;  drgb $000000
  drgb $080000
  drgb $100000
  drgb $180000
  drgb $FFFFFF
  drgb $000800
  drgb $001000
  drgb $001800
  drgb $FFFFFF
  drgb $000008
  drgb $000010
  drgb $000018
  drgb $FFFFFF
  drgb $080808
  drgb $101010
  drgb $181818

gradient_labels:
  db  32,  8,"0",10
  db  48,  8,"2",10
  db  64,  8,"4",10
  db  80,  8,"6",10
  db  96,  8,"8",10
  db 112,  8,"A",10
  db 128,  8,"C",10
  db 144,  8,"E",10
  db   4, 16,"Red",10
  db   4, 48,"Green",10
  db   4, 80,"Blue",10
  db   4,112,"White",0

activity_gradient_bars:
  ld a,LCDCF_ON|BG_NT0|BG_CHR01|LCDCF_OBJON
  ldh [cur_lcdc],a
.restart:
  call clear_gbc_attr

  ; Load tiles and map
  call load_smpte_tiles
  ld de,gradient_rowmap
  call smpte_load_map_SCRN0
  ld hl,_SCRN1
  ld de,gradient_rowmap
  call smpte_load_map
  call gradient_load_oam
  ld de,(CHRRAM0 >> 4)|$20
  ld hl,gradient_labels
  call vwfDrawLabels
  
  ; Add grid to map
  ld hl,_SCRN1
  ld bc,65536-32*18
  .addgridloop:
    ld a,[hl]
    or a
    jr nz,.addgridnz
      ld a,$0C  ; grid tile
    .addgridnz:
    ld [hl+],a
    inc c
    jr nz,.addgridloop
    inc b
    jr nz,.addgridloop

  ld a,IEF_LCDC|IEF_VBLANK
  ldh [rIE],a
  ld a,STATF_LYC
  ldh [rSTAT],a
  ld a,%11110111  ; Set color 0 for backdrop area
  call set_bgp

  ld hl,gradient_objpalette
  ld bc,32*256+low(rOCPS)
  ld a,$82
  call set_gbc_palette

.loop:
  ldh a,[cur_lcdc]
  ldh [vblank_lcdc_value],a
  ldh [stat_lcdc_value],a
  ldh [rLCDC],a
  ld b,helpsect_gradient_color_bars
  call read_pad_help_check
  jr nz,.restart

  ldh a,[new_keys]
  ld b,a
  bit PADB_B,b
  ret nz
  bit PADB_A,b
  jr z,.not_toggle_grid
    ldh a,[cur_lcdc]
    xor LCDCF_BG9C00
    ldh [cur_lcdc],a
  .not_toggle_grid:

  rst wait_vblank_irq
  ld a,8
  ld de,%000000000000001
  call gradient_push_palette
  ld a,40
  ld de,%000000000100000
  call gradient_push_palette
  ld a,72
  ld de,%000010000000000
  call gradient_push_palette
  ld a,104
  ld de,%000010000100001
  call gradient_push_palette
  jr .loop

gradient_load_oam:
  ld hl,SOAM
  ld de,$18FF
  call .threepairpair
  call .threepairpair
  ld a,l
  ld [oam_used],a
  call lcd_clear_oam
  jp run_dma
.threepairpair:
  call .threepair
.threepair:
  ld a,d
  add 8
  ld d,a
  inc e
  call .onepair
  call .onepair
.onepair:
  ld a,d    ; y position
  ld [hl+],a
  ld a,32+8 ; x position
  ld [hl+],a
  ld a,$07  ; tile number
  ld [hl+],a
  ld a,e    ; attribute
  ld [hl+],a
  ld a,d
  ld [hl+],a
  add 8
  ld d,a
  ld a,40+8
  ld [hl+],a
  ld a,$08
  ld [hl+],a
  ld a,e    
  ld [hl+],a
  ret

gradient_push_palette:
  ; Prepare to wait for scanline A
  di
  ld b,a
  ldh [rLYC],a  ; Set scanline to wait for: tile row above transition
  ld a,STATF_LYC
  ldh [rSTAT],a

  ; Construct initial color
  ld h,d
  ld l,e
  add hl,hl
  add hl,hl
  ld bc,7*256+low(rBCPD)
  ld a,$88
  ldh [rBCPS],a  ; seek in palette memory

  halt  ; Wait for LY=LYC
  
  ; Now get ready to wait for hblank interrupts
  ld a,STATF_MODE00
  ldh [rSTAT],a
  .lineloop:
    xor a
    ldh [rIF],a
    halt
    rept 4
      ld a,l
      ldh [c],a
      ld a,h
      ldh [c],a
      add hl,de
    endr
    dec b
    jr nz,.lineloop
  ld a,STATF_LYC
  ldh [rSTAT],a
  reti

; SMPTE color bars & Color bars on gray ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "smpteloader", ROM0
smpte_rowdata:
  db $00,$00,$00,$00,$00,$00,$00,$00,$00,$00, $00,$00,$00,$00,$00,$00,$00,$00,$00,$00  ; color 0
  db $03,$03,$04,$02,$02,$05,$01,$01,$06,$00, $10,$19,$11,$11,$1A,$12,$12,$1B,$13,$13  ; SMPTE1
  db $23,$23,$24,$22,$22,$25,$21,$21,$26,$20, $30,$39,$31,$31,$3A,$32,$32,$3B,$33,$33  ; SMPTE2
  db $43,$43,$43,$C8,$42,$42,$42,$41,$41,$41, $C7,$40,$40,$40,$51,$50,$52,$50,$50,$50  ; SMPTE3
  db $13,$13,$14,$12,$12,$15,$11,$11,$16,$10, $00,$09,$01,$01,$0A,$02,$02,$0B,$03,$03  ; SMPTE1 flipped
  db $00,$00,$00,$00,$02,$02,$17,$18,$27,$28, $37,$38,$47,$48,$57,$58,$67,$68,$77,$78  ; gradient
  db $00,$00,$01,$01,$00,$00,$04,$04,$00,$00, $13,$13,$13,$13,$13,$13,$13,$13,$00,$00  ; pluge 1
  db $00,$00,$01,$01,$00,$00,$04,$04,$00,$00, $12,$12,$12,$12,$12,$12,$12,$12,$00,$00  ; pluge 2
  db $00,$00,$01,$01,$00,$00,$04,$04,$00,$00, $11,$11,$11,$11,$11,$11,$11,$11,$00,$00  ; pluge 3
  db $00,$00,$01,$01,$00,$00,$04,$04,$00,$00, $10,$10,$10,$10,$10,$10,$10,$10,$00,$00  ; pluge 4
  db $01,$02,$03,$92,$91,$21,$22,$23,$B2,$B1, $41,$42,$43,$D2,$D1,$61,$62,$63,$F2,$F1  ; gbc gray ramp top
  db $71,$72,$73,$E2,$E1,$51,$52,$53,$C2,$C1, $31,$32,$33,$A2,$A1,$11,$12,$13,$82,$81  ; gbc gray ramp bottom
  db $03,$03,$03,$03,$03,$03,$03,$03,$03,$03, $03,$03,$03,$03,$03,$03,$03,$03,$03,$03  ; color 3

smpte_rowmap:
  db $B1,$22,$53,$00
ongray_rowmap:
  db $3C,$41,$4C,$44,$3C,$00
gradient_rowmap:
  db $20,$35,$10,$35,$10,$35,$10,$35,$10,$00
pluge_rowmap:
  db $10,$46,$47,$48,$49,$10,$00
pluge_sgb_rowmap:
  db $10,$46,$47,$88,$10,$00
gray_ramp_rowmap::
  db $10,$8A,$8B,$10,$00

smpte_load_map_SCRN0::
  ld hl, _SCRN0
  fallthrough smpte_load_map

;;
; @param DE pointer to rowmap
; @param HL pointer to _SCRN0 or _SCRN1
smpte_load_map::
.rowgrploop:
  ld a,[de]  ; A: which row
  inc de
  or a
  ret z
  push de
  push af  ; Stack: height, rowmap pos

  ; Set DE to smpte_rowdata[A & 0x0F]
  and $0F
  ld e,a
  add a  ; row*2
  add a  ; row*4
  add e  ; row*5
  ld de,smpte_rowdata
  add a  ; row*10
  add a  ; row*20
  jr nc,.rowdata_nowrap1  ; 13 to 15 times 20 exceeds 256
    inc d
  .rowdata_nowrap1:
  add e
  ld e,a
  adc d
  sub e
  ld d, a

  pop af  ; Stack: rowmap pos
  swap a
  and $0F
  ld b,a   ; B: height of this segment in rows; DE: src; HL: dst
  ld c,20
  .rowloop:
    push de
    push bc   ; Stack: height, rowdata start, rowmap pos
    .byteloop:
      ; Copy the attribute
      ld a,1
      ldh [rVBK],a
      ld a,[de]  ; A[7]: hflip; A[6:4]: palette
      swap a
      and $0F
      bit 3,a
      jr z,.nb3
        xor %00101000  ; move bit 3 to bit 5
      .nb3:
      ld [hl],a
      xor a
      ldh [rVBK],a

      ; Copy the tile number
      ; (on mono or SGB this overwrites the attribute)
      ld a,[de]  ; A[3:0]: tilenumber
      and $0F
      ld [hl+],a

      ; Advance to next byte
      inc de
      dec c
      jr nz,.byteloop
    ld de,12
    add hl,de
    pop bc
    pop de  ; Stack: rowmap pos
    dec b
    jr nz,.rowloop
  pop de
  jr .rowgrploop


; There's no wave channel period that is exactly 1 kHz, but there's
; a 500 Hz.  So load the wave RAM with two sine cycles and play that.
def HALF_KHZ equ 2048-131

section "smptebars", ROM0
; The SMPTE bars test shares cbog_palette_* with the Color bars on
; gray palette from the Super Game Boy palette set.
; Color bars on gray uses cbog as palettes 0 and 1
; SMPTE bars uses cbog as palettes 0 and 1, smpte as 2 and 3
; for the castellations, and both for the bottom row as 4 and 5
smpte_palette_dark:
  drgb $101010  ; Green to blue (nonblue replaced with black)
  drgb $C010C0
  drgb $101010
  drgb $1010C0
  drgb $101010  ; Green to white (nonblue replaced with black)
  drgb $10C0C0
  drgb $101010
  drgb $C0C0C0

smpte_palette_darkj:
  drgb $101010  ; Green to blue (nonblue replaced with black)
  drgb $C010C0
  drgb $101010
  drgb $1010C0
  drgb $101010  ; Green to white (nonblue replaced with black)
  drgb $10C0C0
  drgb $101010
  drgb $C0C0C0

smpte_palette_light:
  drgb $101010  ; Green to blue (nonblue replaced with black)
  drgb $FF10FF
  drgb $101010
  drgb $1010FF
  drgb $101010  ; Green to white (nonblue replaced with black)
  drgb $10FFFF
  drgb $101010
  drgb $FFFFFF

smpte_palette_lightj:
  drgb $101010  ; Green to blue (nonblue replaced with black)
  drgb $FF10FF
  drgb $101010
  drgb $1010FF
  drgb $101010  ; Green to white (nonblue replaced with black)
  drgb $10FFFF
  drgb $101010
  drgb $FFFFFF

smpte_palette_both:
  drgb $101010
  drgb $300068  ; purple (Q)
  drgb $FFFFFF  ; bright white
  drgb $002050  ; blue (I)
  drgb $101010  ; shade2 (black)
  drgb $080808  ; shade1 (just below black)
  drgb $181818  ; shade3
  drgb $FF00FF

smpte_palette_bothj:
  drgb $000000
  drgb $300068  ; purple (Q)
  drgb $FFFFFF  ; bright white
  drgb $002050  ; blue (I)
  drgb $000000  ; shade2 (black)
  drgb $000000  ; shade1 (also black)
  drgb $080808  ; shade3
  drgb $FF00FF

cbog_sgb_rects:
  db $04*8+1  ; 1 packet holds up to 2 rectangles
  db 2        ; number of rectangles

  db %00000111  ; bit 0: inside, 1: border, 2: outside
  db %00000101  ; inside and border to palette 1, outside to palette 0
  db 10, 3, 19, 6

  db %00000001  ; bit 0: inside, 1: border, 2: outside
  db %00000101  ; inside and border to palette 0
  db 0, 11, 9, 14

  db 0, 0

activity_barsongray:
  ld a,helpsect_color_bars_on_gray
  db $01  ; was jr smpte_bars_engine but is now LD BC, (the next instruction)
  fallthrough activity_smpte_bars
activity_smpte_bars:
  ld a,helpsect_smpte_color_bars
  fallthrough smpte_bars_engine
smpte_bars_engine:
  ldh [curhelpsect],a
  xor a
  ldh [curpalette],a
  ldh [curvolume],a

.restart:
  call clear_gbc_attr

  ; Load lightness-invariant part of SMPTE bars palette
  ld a,$A0
  ld bc,(2*8)*256+low(rBCPS)
  ld hl,smpte_palette_both
  call set_gbc_palette

  call load_smpte_tiles
  ld de,smpte_rowmap
  ldh a,[curhelpsect]
  cp helpsect_smpte_color_bars
  jr z,.not_load_alternate
    ld de,ongray_rowmap
  .not_load_alternate:
  call smpte_load_map_SCRN0

  call init_apu
  ld hl,waveram_sin2x
  call load_waveram_hl
  ldh a,[curvolume]
  ldh [rNR32],a
  ld a,low(HALF_KHZ)
  ldh [rNR33],a
  ld a,high(HALF_KHZ)
  or $80
  ldh [rNR34],a

  ld a,LCDCF_ON|BG_NT0|BG_CHR01
  ldh [vblank_lcdc_value],a
  ldh [rLCDC],a

  ld a, %11100100
  ldh [rBGP],a  ; set SGB internal
  ldh a, [hw_capability]
  ldh [need_sgb_update], a  ; mark as ready for re-send
  ld hl, cbog_sgb_rects
  call sgb_send_if_sgb

.loop:
  ldh a,[curhelpsect]
  ld b,a
  call read_pad_help_check
  jr nz,.restart

  ldh a,[new_keys]
  ld b,a
  bit PADB_B,b
  jr z, .not_quit
    xor a
    ldh [rNR52], a
    ret
  .not_quit:

  ; 0: NTSC setup, 75%
  ; 1: no setup, 75%
  ; 2: NTSC setup, 100%
  ; 3: no setup, 100%
  ldh a,[curpalette]
  bit PADB_UP,b
  jr z,.notoggle75100
    xor $02
    jr .have_new_curpalette
  .notoggle75100:
  bit PADB_A,b
  jr z,.notogglesetup
    xor $01
  .have_new_curpalette:
    ldh [curpalette],a
    ldh a, [hw_capability]
    ldh [need_sgb_update], a
  .notogglesetup:

  bit PADB_SELECT,b
  jr z,.notogglevol
    ldh a,[curvolume]
    xor $20
    ldh [curvolume],a
  .notogglevol:

  rst wait_vblank_irq
  ; load volume after vsync to ensure the sound starts at the
  ; same time as the picture
  ldh a,[curvolume]
  ldh [rNR32],a

  ; Load palette and tiles
  ld hl,cbog_palette_dark
  ldh a, [curpalette]  ; 75% setup, 75% no setup, 100% setup, 100% no setup
  ld e, a
  ld a,$80
  call .send_e
  ld bc, smpte_palette_light - (cbog_palette_light + 2*8)
  add hl, bc
  ld a,$90
  ld bc,(2*8)*256+low(rBCPS)
  call set_gbc_palette
  ld hl, smpte_palette_both
  ldh a, [curpalette]
  and $01
  ld e, a
  ld a,$A0
  call .send_e

  ld hl, smpte_palette_both

  ldh a, [need_sgb_update]
  rra
  jr nc, .not_send_sgb
    ldh a, [curpalette]
    add a
    add (cbog_palette_dark-sgb_all_palettes)/8
    ld b, a
    ld c, a
    inc c
    ld de, $0000
    call sgb_set_palettes_bcde
  .not_send_sgb:

  jr .loop

  ; Sends 8 colors at HL+E*16 to the background palette at
  ; index A ($80-$BF)
.send_e:
  ld d, 0
  swap e
  add hl, de
  ld bc,(2*8)*256+low(rBCPS)
  jp set_gbc_palette


; Color bleed test ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; This shares code with Full screen stripes in stills.z80.

section "colorbleed", ROM0
sgb_bleed_rects:
  db $04*8+2  ; 3 packets hold up to 5 rectangles
  db 4        ; number of rectangles

  ; Rows 0-10: palette 0 | 2
  db %00000111  ; bit 0: inside, 1: border, 2: outside
  db %00001010  ; inside and border to palette 2, outside to palette 0
  db 10, 0, 19, 10  ; l, t, r, b, inclusive

  ; Rows 4-6: palette 2
  db %00000001
  db %00000001
  db 0, 4, 19, 6

  ; Rows 11-13: palette 3
  db %00000001
  db %00000011
  db 0, 11, 19, 13

  ; Rows 14-17: palette 3 | 0
  db %00000001
  db %00000011
  db 0, 14, 9, 17
  db 0, 0, 0, 0, 0, 0  ; Rect 5: Unused

bleed_tile_ids:
  db $10, $10
  db $10, $11
  db $11, $11
  db $11, $11
  db $10, $12
  db $FF

gbc_bleed_attrs:
  db $00, $02
  db $01, $01
  db $00, $02
  db $03, $03
  db $03, $00
  db $FF

;;
; @param DE pointer to 10-entry map
draw_bleed_rects:
  ; Load plane 0
  ld hl,_SCRN0
  xor a  ; blank tile
  ld c,32+1
  rst memset_tiny
  .p0rowgrploop:
    ld b, 3
    .p0rowloop:
      ld a, [de]
      inc de
      ld c,8
      rst memset_tiny
      xor a   ; Blank tile
      ld [hl+], a
      ld [hl+], a
      ld a, [de]  ; Right half pattern
      dec de
      ld c,8
      rst memset_tiny
      xor a  ; blank tile
      ld c,14
      rst memset_tiny
      dec b
      jr nz,.p0rowloop
    ld c,32  ; blank row
    rst memset_tiny

    ; Advance to next group of rows
    inc de
    inc de
    ld a, [de]
    add a
    jr nc,.p0rowgrploop
  ret

load_color_bleed::
  ld de, bleed_tile_ids
  call draw_bleed_rects

  ; Load GBC attributes
  ldh a, [hw_capability]
  rlca
  jr nc, .load_sgb_attrs
  ; ld a, 1  ; bit 0 already set!
  ldh [rVBK],a
  ld de, gbc_bleed_attrs
  call draw_bleed_rects
  ld hl,_SCRN1
  ld c,7
  ld a,3
  rst memset_tiny

  ; Load palette
  ld a,$80
  ldh [rVBK],a
  ld bc,(color_bleed_palette_end-color_bleed_palette)*256+low(rBCPS)
  ld hl,color_bleed_palette
  jp set_gbc_palette

.load_sgb_attrs:
  ld a, %11100100
  ldh [rBGP], a
  ld hl, sgb_bleed_rects
  call sgb_send
  ld bc, $0405
  ld de, $0607
  jp sgb_set_palettes_bcde
