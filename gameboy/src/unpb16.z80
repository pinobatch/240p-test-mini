;
; PB16 decompression for Game Boy
; Copyright 2018 Damian Yerrick
; 
; This software is provided 'as-is', without any express or implied
; warranty.  In no event will the authors be held liable for any damages
; arising from the use of this software.
; 
; Permission is granted to anyone to use this software for any purpose,
; including commercial applications, and to alter it and redistribute it
; freely, subject to the following restrictions:
; 
; 1. The origin of this software must not be misrepresented; you must not
;    claim that you wrote the original software. If you use this software
;    in a product, an acknowledgment in the product documentation would be
;    appreciated but is not required.
; 2. Altered source versions must be plainly marked as such, and must not be
;    misrepresented as being the original software.
; 3. This notice may not be removed or altered from any source distribution.
;
include "src/global.inc"
def CHRRAM0 equ $8000
def CHRRAM2 equ $9000


  rsset hLocals
def pb16_byte0 rb 1

section "pb16", ROM0

; The PB16 format is a starting point toward efficient RLE image
; codecs on Game Boy and Super NES.
;
; 0: Load a literal byte
; 1: Repeat from 2 bytes ago

pb16_unpack_packet:
  ; Read first bit of control byte.  Treat B as a ring counter with
  ; a 1 bit as the sentinel.  Once the 1 bit reaches carry, B will
  ; become 0, meaning the 8-byte packet is complete.
  ld a,[de]
  inc de
  scf
  rla
  ld b,a
.byteloop:
  ; If the bit from the control byte is clear, plane 0 is is literal
  jr c,.p0_is_run
  ld a,[de]
  inc de
  ldh [pb16_byte0],a
  db $DA  ; was jr nc, .have_p0; now jp c, dont_care
.p0_is_run:
  ldh a,[pb16_byte0]
.have_p0:
  ld [hl+],a

  ; Read next bit.  If it's clear, plane 1 is is literal.
  ld a,c
  sla b
  jr c,.have_p1
.p1_is_copy:
  ld a,[de]
  inc de
  ld c,a
.have_p1:
  ld [hl+],a

  ; Read next bit of control byte
  sla b
  jr nz,.byteloop
  ret

; Tile loaders used in multiple places, to optimize JP to JR

load_common_obj_tiles::
  ld hl,CHRRAM0
  fallthrough load_common_tiles_hl
load_common_tiles_hl::
  ld de,bggfx_chr
  ld b,32
  jr pb16_unpack_block

;;
; Loads the following tiles into CHR RAM
; 00: c0
; 01: c1
; 02: c2
; 03: c3
; 04: 7 c3, 1 c2
; 05: 6 c2, 2 c1
; 06: 5 c1, 3 c0
; 07: 4 c0, 4 c1
; 08: 4 c2, 4 c3
; 09: 06 flipped
; 0A: 05 flipped
; 0B: 04 flipped
; 0C: Grid square
load_smpte_tiles::
  ld hl,CHRRAM0
  ld de,smptetiles_chr
  ld b,13
  jr pb16_unpack_to_CHRRAM0

load_backlight_chr::
  ld de,backlight_chr
  ld b,5
  fallthrough pb16_unpack_to_CHRRAM0

pb16_unpack_to_CHRRAM0::
  ld hl, CHRRAM0
  fallthrough pb16_unpack_block

;;
; Unpacks 2*B packets from DE to HL (opposite direction vs. memcpy),
; producing 8 bytes per packet.
; About 127 cycles (2 scanlines) per 8-byte packet; filling CHR RAM
; thus takes (6144/8)*127 = about 97536 cycles or 93 ms
; @return B: 0; DE: end of source; HL: end of destination
pb16_unpack_block::
  ; Prefill with zeroes
  xor a
  ldh [pb16_byte0],a
.packetloop:
  ld c,a
  push bc
  call pb16_unpack_packet
  call pb16_unpack_packet
  ld a,c
  pop bc
  dec b
  jr nz,.packetloop
  ret
