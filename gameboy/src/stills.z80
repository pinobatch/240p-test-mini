;
; Still image (or mostly so) tests for 240p test suite
; Copyright 2018 Damian Yerrick
;
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License along
; with this program; if not, write to the Free Software Foundation, Inc.,
; 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.
;
include "src/gb.inc"
include "src/global.inc"

  rsset hTestState
def curpalette rb 1
def curtileid rb 1
def curscry rb 1
def curlcdc rb 1
def lastlcdc rb 1
def customred   rb 1
def customgreen rb 1
def customblue  rb 1
def bleed_help_page rb 1
def curframe_bcd rb 1

; Gray ramp ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "grayramp",ROM0

gray_ramp_sgb_rects:
  db $04*8+2  ; 3 packets hold up to 5 rectangles
  db 5      ; number of rectangles

  ; Rectangle 1 of 5: Set whole screen as follows
  ; [0 2 2 0]
  ; [0 2 2 0]
  db %00000111  ; bit 0: inside, 1: border, 2: outside
  db %00001010  ; inside and border to palette 2, outside to palette 0
  db 5, 1, 14, 16  ; l, t, r, b, inclusive

  ; Rectangle 2 of 5: Set second to palette 1
  db %00000001
  db %00000001
  db 5, 1, 9, 8

  ; Rectangle 3 of 5: Set seventh to palette 1
  db %00000001
  db %00000001
  db 10, 9, 14, 16

  ; Rectangle 4 of 5: Set fourth to palette 3
  db %00000001
  db %00001111
  db 15, 1, 19, 8

  ; Rectangle 5 of 5: Set fifth to palette 3
  db %00000001
  db %00000011
  db 0, 9, 4, 16

;;
; Expand A tiles from DE to HL, duplicating each 4-byte group 4 times
; DE must be 4 byte aligned
load_gray_ramp_tiles:
  local hTilesLeft

  ldh [.hTilesLeft],a
  lb bc, 4, 4  ; repeating each C-byte portion B times
  .rowpairloop:
    push bc
    .byteloop:
      ld a,[de]
      inc e
      ld [hl+],a
      dec c
      jr nz,.byteloop
    pop bc
    ld a,e
    sub c
    ld e,a
    dec b
    jr nz,.rowpairloop

  ; Move to next 4-byte group
  add c
  ld e,a
  adc d
  sub e
  ld d, a

  ldh a,[.hTilesLeft]
  dec a
  jr nz,load_gray_ramp_tiles
  ret

load_gray_ramp_sgb:
  ld de, grayramp_sgb_chr
  ld a, 9
  call load_gray_ramp_tiles

  ; Now the tilemap
  ; Row 0
  ld hl,_SCRN0
  call oneblanktilerow

  ; Rows 1-8
  ld c, 8
  .tophalfrowloop:
    ld a, 1
    ld de, $0605
    call gray_ramp_row_cmp_d_sub_e
    dec c
    jr nz,.tophalfrowloop
  ; Rows 9-16
  ld c, 8
  .bottomhalfrowloop:
    ld de, $0A05
    ld a, e
    call gray_ramp_row_cmp_d_sub_e
    dec c
    jr nz,.bottomhalfrowloop

  call oneblanktilerow
  ld a,%11100100
  ldh [rBGP], a
  ld hl, gray_ramp_sgb_rects
  call sgb_send
  ld bc, $0809
  ld de, $0A0B
  jp sgb_set_palettes_bcde

load_gray_ramp:
  ldh a, [hw_capability]
  add a
  jr c, load_gray_ramp_gbc
  jr nz, load_gray_ramp_sgb
  fallthrough load_gray_ramp_mono
load_gray_ramp_mono:

  ; Tiles 1-24 are compressed: 2 rows (4 bytes) are written 4 times
  ld de,grayramp_chr
  ld a,24
  call load_gray_ramp_tiles

  ; Now the tilemap
  ; Row 0
  ld hl,_SCRN0
  call oneblanktilerow

  ; Rows 1-8
  ld c,8
  .tophalfrowloop:
    xor a
    ld de, $1313
    call gray_ramp_row_cmp_d_sub_e
    dec c
    jr nz,.tophalfrowloop
  ; Rows 9-16
  ld c,8
  .bottomhalfrowloop:
    ld b,20
    ld de,grayramp_bottomhalfmap
    .bottomhalfbyteloop:
      ld a,[de]
      inc de
      ld [hl+],a
      dec b
      jr nz,.bottomhalfbyteloop
    call add_hl_12
    dec c
    jr nz,.bottomhalfrowloop
  ld a,%00011011
  ldh [rBGP], a
  fallthrough oneblanktilerow ; Row 19

oneblanktilerow:
  xor a
  fallthrough oneconsttilerow
oneconsttilerow:
  ld c,20
  rst memset_tiny
  fallthrough add_hl_12
add_hl_12:
  ld de,12
  add hl,de
  ret

;;
; Writes a row of 20 increasing tile numbers to the tilemap,
; optionally looping.
; @param A first tile ID
; @param D first tile ID that is not part of the loop
; @param E length of loop
; @param HL start of row
; @return HL at start of next row; B=0, DE=12
gray_ramp_row_cmp_d_sub_e:
  ld b, 20
  .byteloop:
    ld [hl+], a
    inc a
    cp d
    jr c,.nosube
      sub e
    .nosube:
    dec b
    jr nz, .byteloop
  jr add_hl_12

load_gray_ramp_gbc:

  ; Tiles 1-3 are a cycle of the gray ramp, and tile 4-5 is solid
  ; to fill in half of the color 0 tile
  ld b,7
  ld de,grayramp_chr_gbc
  call pb16_unpack_block

  ; Set gray ramp palette
  ld a,$80
  ldh [rBCPS],a
  ldh [rOCPS],a
  ld b,0
  .palloop:
    ; calculate 01237654
    ld a,b
    bit 2,a
    jr z,.notdesc
      xor $03
    .notdesc:

    ; calculate $401*a
    ld e,a
    add a
    add a
    ld d,a
    ld l,a

    ; add $20*b
    ld h,0
    add hl,hl
    add hl,hl
    add hl,hl
    add hl,de

    ; and write to the VCE
    ld a,l
    ldh [rBCPD],a
    ldh [rOCPD],a
    ld a,h
    ldh [rBCPD],a
    ldh [rOCPD],a
    inc b
    bit 5, b
    jr z,.palloop

  ld de,gray_ramp_rowmap
  call smpte_load_map_SCRN0

  ; Because each vertical stripe is 5 pixels wide, the change from
  ; e.g. palette 0 to 1 occurs in the middle of a tile.  This would
  ; cause attribute clash.  So fill the gap with sprites using
  ; solid tile $04.
  ; B: x, C: y; D: attr counter; E: attr xor
  ld hl,SOAM
  ld bc,24*256+24
  ld de,$0081
  .filloamloop:
    ld a,b
    ld [hl+],a  ; write Y
    ld a,c
    ld [hl+],a  ; write X
    add 40
    ld c,a
    ld a,$04
    ld [hl+],a  ; write tile number
    ld a,d
    xor e
    ld [hl+],a  ; write attribute
    ld a,d
    add 2
    cp 8
    jr c,.have_new_oam_attr
      ; Prepare for next row, or return if there isn't one
      ld a,16
      add b
      cp 24+128
      ret nc
      ld b,a
      cp 24+64
      jr nz,.oamnothalfway
        ld e,$A6
      .oamnothalfway:
      ld c,24
      xor a

    .have_new_oam_attr:
    ld d,a
    jr .filloamloop

activity_gray_ramp::
.restart:
  call clear_gbc_attr
  call lcd_clear_oam
  xor a
  ld hl,CHRRAM0
  ld c,16
  rst memset_tiny
  ; The loaders expect hl = CHRRAM0+16
  call load_gray_ramp

  ; Turn on rendering
  call run_dma
  ld a,LCDCF_ON|BG_NT0|BG_CHR01|OBJ_8X16
  ldh [vblank_lcdc_value],a
  ldh [rLCDC],a

  ; Now just wait for a B press.  Fortunately, we don't have
  ; to deal with half B presses or parallel universes here.
.loop:
  ld b,helpsect_gray_ramp
  call read_pad_help_check
  jr nz,.restart
  rst wait_vblank_irq
  ldh a,[new_keys]
  bit PADB_B,a
  jr z,.loop
  ret

; Linearity ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

def linearity_SCRN0_pivot equ _SCRN0 + _SCRN0 + 32 * 17 + 19

section "linearity",ROM0
activity_linearity::
  ld a,%00011011
  ldh [curpalette],a
  ld a,LCDCF_ON|BG_NT0|BG_CHR21
  ldh [curlcdc],a

.restart:
  call clear_gbc_attr

  ld l,5
  ld bc,CHRRAM2
  call allhuffdata_unpack_iu_file_l
  ld hl,SCRN_TMP
  ld de,_SCRN0
  ld bc,256*10+9
  call load_nam

  ; Reflect top left quadrant of tilemap into top right
  ld de,_SCRN0
  ld b, 9
  .nam_hflip_rowloop:
    ld c, 10
    ld hl, 19
    add hl, de
    .nam_hflip_byteloop:
      ld a, [de]
      inc e
      xor $20
      ld [hl-], a
      dec c
      jr nz, .nam_hflip_byteloop
    ld hl, 32 - 10
    add hl, de
    ld d, h
    ld e, l
    dec b
    jr nz,.nam_hflip_rowloop

  ; Reflect top of tilemap into bottom
  ld de,_SCRN0
  ld b, 9
  .nam_vflip_rowloop:
    ld a, low(linearity_SCRN0_pivot)
    sub e
    ld l, a
    ld a, high(linearity_SCRN0_pivot)
    sbc d
    ld h, a
    ld c, 20
    .nam_vflip_byteloop:
      ld a, [de]
      inc e
      xor $5F
      ld [hl-], a
      dec c
      jr nz, .nam_vflip_byteloop
    ld hl, 32 - 20
    add hl, de
    ld d, h
    ld e, l
    dec b
    jr nz,.nam_vflip_rowloop

  ; Reflect tiles horizontally
  ld de, $9000
  ld hl, $9200
  .chr_hflip_loop:
    ld a, [de]
    inc de
    scf
    adc a
    .chr_hflip_bitloop:
      rr b
      add a
      jr nz, .chr_hflip_bitloop
    ld a, b
    ld [hl+], a
    ld a, h
    cp $94
    jr nz, .chr_hflip_loop

  ; Reflect tiles vertically
  ld de, $9000
  ld hl, $97FE
  .chr_vflip_loop:
    ld a, [de]
    inc e
    ld [hl+], a
    ld a, [de]
    inc de
    ld [hl-], a
    dec hl
    dec l
    ld a, d
    cp $94
    jr nz, .chr_vflip_loop

  ; Form full grid, copying already loaded tiles from CHRRAM2
  ; (default) to CHRRAM0 (nondefault)
  ld hl,CHRRAM2
  ld de,CHRRAM0
  ld c,128
  ld b,7
  .gridtileloop:
    push bc
    ; Top row: Copy plane 1 to plane 0 and set plane 1 to $FF
    inc hl
    ld a,[hl+]
    ld [de],a
    inc de
    ld a,$FF
    ld [de],a
    inc de
    ld b,7

    .gridsliverloop:
      ; Other rows: Copy plane 1 bit 7 and plane 0 bits 6-0 to
      ; plane 0, and plane 1 OR $80 to plane 1.
      ld a,[hl+]
      ld c,a
      ld a,[hl]
      xor c
      and $80
      xor c
      ld [de],a
      inc de
      ld a,[hl+]
      or $80
      ld [de],a
      inc de
      dec b
      jr nz,.gridsliverloop
  
    pop bc
    dec c
    jr nz,.gridtileloop

  call sgb_set_palette_gray
  ldh a,[curlcdc]
  ldh [vblank_lcdc_value],a
  ldh [rLCDC],a

.loop:
  ld b,helpsect_linearity
  call read_pad_help_check
  jp nz,.restart
  rst wait_vblank_irq
  ldh a,[curpalette]
  call set_bgp

  ; Process input
  ldh a,[new_keys]
  ld b,a
  call select_inverts_palette

  bit PADB_A,b
  jr z,.not_A
    ldh a,[curlcdc]
    xor BG_CHR21^BG_CHR01
    ldh [curlcdc],a
    ldh [vblank_lcdc_value],a
  .not_A:

  bit PADB_B,b
  jr z,.loop
  ret


; Sharpness ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
section "sharpness",ROM0

  if 0  ; Obsolete since IUR
;;
; Loads a PB16-compressed 20x18-tile nametable to VRAM, using
; SCRN_TMP as a temporary buffer.
; @param DE source
; @param HL destination (usually _SCRN0 or _SCRN1)
; @param B width (load_full_pb16_nam forces 20)
; @param C height (load_full_pb16_nam forces 18)
load_full_pb16_nam::
  ld bc,20*256+18
  fallthrough load_pb16_nam
load_pb16_nam::
  push bc
  push hl
  ld hl,SCRN_TMP
  push hl
  ld b,368/16
  call pb16_unpack_block
  pop de
  pop bc
  pop hl
  jp load_nam
  endc

activity_sharpness::
  ld a,%00011011
  ldh [curpalette],a
  ld a,LCDCF_ON|BG_NT0|BG_CHR21
  ldh [curlcdc],a

.restart:
  call clear_gbc_attr

  ; Load primary picture
  ld bc, CHRRAM2
  ld l, 4
  call allhuffdata_unpack_iu_file_l
  ld hl,SCRN_TMP
  ld de,_SCRN0
  call load_full_nam

  ; Load secondary picture
  ld hl,sharpness_brick_tile
  ld de,CHRRAM2+$7F0
  call memcpy_pascal16
  ld h,$7F
  ld de,_SCRN1
  ld bc,32*18
  call memset

  ldh a,[curlcdc]
  ldh [vblank_lcdc_value],a
  ldh [rLCDC],a
  cpl
  ldh [lastlcdc], a

.loop:
  ld b,helpsect_sharpness
  call read_pad_help_check
  jr nz,.restart
  rst wait_vblank_irq

  ; Load the appropriate GB/GBC palette for what is showing
  ldh a,[curlcdc]
  and LCDCF_BG9C00
  jr z,.load_normal_palette
    ld a,%11100100
    ldh [rBGP],a
    ld a,$82
    ld bc,6*256+LOW(rBCPS)
    ld hl,sharpness_bricks_gbc_palette
    call set_gbc_palette
    jr .done_loading_palette
  .load_normal_palette:
    ldh a,[curpalette]
    call set_bgp
  .done_loading_palette:

  ; If it corresponds to a change in the SGB palette, dew it
  ldh a, [lastlcdc]
  ld c, a
  ldh a, [curlcdc]
  ldh [lastlcdc], a
  ld b, a
  xor c
  and LCDCF_BG9C00
  ; B: new LCDC value; C: old LCDC value;
  ; A: LCDCF_BG9C00 if changed else 0
  jr z, .no_sgb_palette_change
    and b
    jr z, .sgb_palette_not_zero
      ld a, 3
    .sgb_palette_not_zero:
    ld b, a
    call sgb_set_palette_b
  .no_sgb_palette_change:

  ; Process input
  ldh a,[new_keys]
  ld b,a
  call select_inverts_palette

  bit PADB_A,b
  jr z,.not_A
    ldh a,[curlcdc]
    xor LCDCF_BG9C00
    ldh [curlcdc],a
    ldh [vblank_lcdc_value],a
  .not_A:

  bit PADB_B,b
  jr z,.loop
  ret

;;
; If Select, invert curpalette
; @param B a bitfield made of PADF_ constants
; @return A: new curpalette
select_inverts_palette::
  ldh a,[curpalette]
  bit PADB_SELECT,b
  ret z
  cpl
  ldh [curpalette],a
  ret
  

; Solid screen ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "solidscreen",ROM0
solid_screen_constants:
  drgb $000000
  drgb $FF0000
  drgb $00FF00
  drgb $0000FF
activity_solid_screen::
  xor a
  ldh [curpalette],a
  ld a,31
  ldh [customred],a
  ldh [customgreen],a
  ldh [customblue],a
  ld a,$80
  ldh [curscry],a

.restart:
  call clear_gbc_attr
  call clear_sgb_attr
  call sgb_set_palette_gray

  ; Tile 0: Blank screen in color 0
  ; Tiles 1-0
  ld hl,CHRRAM2
  ld c,16
  xor a
  rst memset_tiny
  dec a
  ldh [lastlcdc], a  ; lastlcdc: nonzero if color changed
  ld c,9*16
  rst memset_tiny

  ; Create VWF canvas at X=160-184, which only GBC/SGB will display
  call clear_scrn0_to_0
  ld a,1
  ldxy hl, 20, 1
  ld b,3  ;  3 rows tall
  ld de,32-3
  .canvasinitrowloop:
    ld c,3
    call memset_inc
    add hl,de
    dec b
    jr nz,.canvasinitrowloop

  ld a,%11000000
  call set_bgp
  ld a,LCDCF_ON|BG_NT0|BG_CHR21
  ldh [vblank_lcdc_value],a
  ldh [rLCDC],a

.loop:
  ld b,helpsect_solid_screen
  call read_pad_help_check
  jr nz,.restart
  rst wait_vblank_irq

  ; Write color to both GB and GBC palette ports
  ldh a, [hw_capability]
  bit 7, a
  jr z, .set_color_not_gbc
    call calc_custom_color
    ld a,$80
    ldh [rBCPS],a
    ld a,l
    ldh [rBCPD],a
    ld a,h
    ldh [rBCPD],a
    xor a  ; make (harmless) DMG write instead of SGB write
  .set_color_not_gbc:
  rra
  ldh a, [curpalette]
  jr nc, .set_color_dmg
    ; Super Game Boy: Write color only if changed
    ldh a, [lastlcdc]
    or a
    jr z, .custom_color_written
    call calc_custom_color

    ; Send this solid color to the SGB
    ld a, $00*8+1
    ld [help_line_buffer+0], a
    ld a, l
    ld [help_line_buffer+1], a
    ld a, h
    ld hl,help_line_buffer+2
    ld [hl+], a
    ; Add white and black for the box at top right
    ld a, $FF
    ld [hl+], a
    ld [hl+], a
    ld [hl+], a
    ld [hl+], a
    xor a
    ldh [lastlcdc], a
    ld c, 9
    rst memset_tiny
    ld hl, help_line_buffer
    call sgb_send

    ld a, %11100100
  .set_color_dmg:
  ldh [rBGP], a
  .custom_color_written:

  ; $80: Scroll to x=32 away from RGB control
  ; $00: Scroll to x=0 with RGB control
  ldh a,[curscry]
  and $80
  xor $80
  rra
  rra
  ldh [rSCX],a

  ; Choose among 5 palettes on GBC or 4 on GB
  ldh a,[new_keys]
  bit PADB_B,a
  ret nz  ; quit now if exit
  ld b,a
  ld c,4
  ldh a, [hw_capability]
  or a
  jr z, .not_5_palettes
    ; A on white screen on GBC or SGB: Toggle custom mode
    bit PADB_A,b
    jr z,.not_toggle_custom
    ldh a,[curpalette]
    or a
    jr nz,.not_toggle_custom
      ldh a,[curscry]
      xor $80
      ldh [curscry],a
    .not_toggle_custom:
    ld c,5
  .not_5_palettes:

  ; Process input
  ldh a,[curscry]
  rla
  jr c,.not_custom_control

    ; Autorepeat for left and right keys
    ld b,PADF_LEFT|PADF_RIGHT
    call autorepeat_60hz_if_sgb
    ldh a,[new_keys]
    ld b,a
    ldh a,[curscry]

    ; At this point: A is which row is selected, and B is buttons.
    ; Handle up and down presses
    bit PADB_DOWN,b
    jr z,.not_custom_next_row
      inc a
    .not_custom_next_row:
    bit PADB_UP,b
    jr z,.not_custom_prev_row
      dec a
    .not_custom_prev_row:
    cp 3
    jr c,.custom_y_in_bounds
      ldh a,[curscry]
    .custom_y_in_bounds:
    ldh [curscry],a

    add low(customred)
    ld c,a
    ldh a, [c]
    ld e, a  ; E = last value
    bit PADB_RIGHT,b
    jr z,.not_custom_increase
      inc a
    .not_custom_increase:
    bit PADB_LEFT,b
    jr z,.not_custom_decrease
      dec a
    .not_custom_decrease:
    ; if the new value is valid, write it back
    cp 32
    jr nc, .custom_no_change
      ldh [c],a
      xor e  ; set E iff changed
      ldh [lastlcdc], a
    .custom_no_change:

    call solid_update_rgb
    jr .input_done
  .not_custom_control:
    bit PADB_LEFT,b
    jr z,.not_left
      ldh a,[curpalette]
      or a
      jr nz,.notwrapprev
        ld a,c
      .notwrapprev:
      dec a
      jr .have_new_curpalette
    .not_left:

    bit PADB_RIGHT,b
    jr z,.not_right
      ldh a,[curpalette]
      inc a
      cp c
      jr c,.notwrapnext
        xor a
      .notwrapnext:
    .have_new_curpalette:
      ldh [curpalette],a
      ld a, c
      ldh [lastlcdc], a
    .not_right:
  .input_done:
  jp .loop

calc_custom_color:
  ldh a, [curpalette]
  or a
  jr z,.is_custom
    ld de,solid_screen_constants-2
    jp de_index_a
  .is_custom:
  ld h,a     ; H = 0
  ldh a,[customgreen]
  add a,a
  add a,a
  add a,a
  ld l,a
  add hl,hl
  add hl,hl  ; HL = G * 32
  ldh a,[customred]
  ld e,a             ; E = R
  ldh a,[customblue]
  add a
  add a              ; A = B * 4
  ld d,a             ; DE = R+B*1024
  add hl,de
  ret

solid_update_rgb:
  ; Draw labels for Red, Green, and Blue
  call vwfClearBuf
  ld a,"R"
  ld b,6+24*0
  call vwfPutTile
  ld a,"G"
  ld b,6+24*1
  call vwfPutTile
  ld a,"B"
  ld b,6+24*2
  call vwfPutTile

  ; Draw cursor
  ldh a,[curscry]
  ld b,a
  add a  ; A=component*2
  add b  ; A=component*3
  add a  ; A=component*6
  add a  ; A=component*12
  scf
  adc a  ; A=component*24+1
  ld b,a
  ld a,">"
  call vwfPutTile

  ; Draw red, green, and blue values
  ld de,19 * 256 + low(customred)
.componentloop:
  push de
  ld c,e
  ldh a, [c]
  call bcd8bit_ac  ; A: tens; C: ones
  jr z,.lessthanten
    push bc
    add "0"
    ld l,a
    ld a,d
    sub 5
    ld b,a  ; B:x, L: ascii tens, stack: ones digit, Y and hramptr
    ld a,l
    call vwfPutTile
    pop bc   ; Restore ones
    pop de   ; Restore Y and hramptr
    push de
  .lessthanten:
  ld a,c
  add "0"
  ld b,d
  call vwfPutTile
  pop de
  inc e
  ld a,24
  add d
  ld d,a
  cp 24*3
  jr c,.componentloop

  ; Push tiles to VRAM during blanking  
  ld hl,CHRRAM2+1*16
  ld c,9
  jp vwfPutBufHBlank

; CPS style grid ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "cpsgrid",ROM0
cps_grid_gbc_palette1:
  drgb $000000
  drgb $FF0000
  drgb $FF0000
  drgb $FFFFFF

activity_cps_grid::
  ld a,%00011011
  ldh [curpalette],a

.restart:
  call clear_gbc_attr

  ld hl,CHRRAM2
  ld de,cps_grid_chr
  ld b,sizeof_cps_grid_chr/16
  call pb16_unpack_block

  ; Draw grid
  ld hl,_SCRN0
  ld b,0  ; top red row
  call cps_grid_two_rows
  ld bc,$0407
  .rowloop:
    push bc
    call cps_grid_two_rows
    pop bc
    dec c
    jr nz,.rowloop
  ld b,c  ; bottom red row
  call cps_grid_two_rows

  ld a,LCDCF_ON|BG_NT0|BG_CHR21
  ldh [vblank_lcdc_value],a
  ldh [rLCDC],a
  ld b, 2
  call sgb_set_palette_b

.loop:
  ld b,helpsect_grid
  call read_pad_help_check
  jr nz,.restart
  rst wait_vblank_irq

  ; Set GB palette
  ldh a,[curpalette]
  ldh [rBGP],a

  ; Set GBC palette
  ld hl, cps_grid_gbc_palette0
  and %00001000
  jr z, .ispal0
    ld hl, cps_grid_gbc_palette1
  .ispal0:
  ld bc,8*256+low(rBCPS)
  ld a,$80
  call set_gbc_palette

  ; Process input
  ldh a,[new_keys]
  ld b,a
  call select_inverts_palette

  bit PADB_B,b
  jr z,.loop
  ret

;;
; Draws one row of the CPS-2 grid.
cps_grid_two_rows:
  call .one_row
  ; Move to bottom half
  inc b
  inc b
.one_row:
  ; Left column
  ld a,b
  and $02
  ld [hl+],a
  inc a
  ld [hl+],a

  ; Center 8 columns
  ld c,16
  ld a,b
  .colloop:
    ld [hl+],a
    xor a,1
    dec c
    jr nz,.colloop

  ; Right column
  ld a,b
  and $02
  ld [hl+],a
  inc a
  ld [hl+],a
  inc a

  ; Move to next row
  ld de,12
  add hl,de
  ret

; Full screen stripes ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

section "fullstripes",ROM0,align[3]
bleed_types:
  db %00000000  ; horizontal
  db %11111111  ; horizontal xor
  db %01010101  ; vertical
  db %00000000  ; vertical xor
  db %01010101  ; diagonal
  db %11111111  ; vertical xor
word_frame:
  db "Frame ",0

;;
; @param A index into bleed_types
; @param B starting XOR value (alternates $00 and $FF)
; Tile $10: colors 0 and 1
load_bleed_frame_a:
  ld de,CHRRAM0+16*$10
  ld hl,bleed_types
  add a,a
  add a,l
  ld l,a
  ld a,[hl+]  ; A = starting byte
  xor b       ; A = starting byte this frame
  ld c,[hl]   ; C = xor value by line
  ld h,d
  ld l,e
  ld b,8      ; bytes left in tile
  ld d,0      ; plane 1 value
  .loop:
    ld [hl+],a
    ld [hl],d
    inc l
    xor c
    dec b
    jr nz,.loop

  ; Construct color 2 tile
  ld [hl], d
  inc l
  ld d, h
  ld e, l
  ld l, 0  ; seek to start of tile
  ld bc, 15
  call memcpy
  
  ; Construct colors 1 and 2 tile
  ld b, 8
  ld l, 0
  .color12loop:
    ld a, [hl+]
    inc l
    ld [de], a
    inc e
    cpl
    ld [de], a
    inc e
    dec b
    jr nz, .color12loop
  ret

activity_color_bleed::
  ld a,helpsect_color_bleed
  db $01  ; ld bc, next_opcode
  fallthrough activity_full_stripes
activity_full_stripes::
  ld a,helpsect_full_screen_stripes
  fallthrough full_stripes_common
full_stripes_common::
  ldh [bleed_help_page],a
  xor a
  ldh [curtileid],a
  ldh [curscry],a
  ldh [curpalette],a
  ldh [curframe_bcd],a
  ld a,LCDCF_ON|BG_NT0|BG_CHR01|LCDCF_WIN9C00
  ldh [curlcdc],a
.restart:
  call clear_gbc_attr

  ; Hide background (tile 16) until ready
;  xor a  ; A is 0 after clear_gbc_attr
  ld hl,CHRRAM0+256
  ld c,16
  rst memset_tiny

  ; Make VWF window for frame number at (0, 0) on window
  ld hl,CHRRAM0
  ld c,112  ; 1 tile blank, 6 tiles window
  ; A is still 0
  rst memset_tiny
  ld hl,_SCRN1
  ld c,6
  ; A is still 0
  call memset_inc
  xor a
  ld [hl+], a

  ; Position the window
  ld a,167-48
  ldh [rWX],a
  ld a,144-8
  ldh [rWY],a

  ldh a,[bleed_help_page]
  cp helpsect_color_bleed
  jr z,.is_color_bleed
    ; Loading specific to "Full screen stripes"

    ; Blow away the tilemap
    ld h,16
    call clear_scrn0_to_h
    ld a,%00000011
    call set_bgp
    call sgb_set_palette_gray
    jr .specific_loading_done
  .is_color_bleed:
    call load_color_bleed
  .specific_loading_done:

  ; Turn on LCD
  ld a, STATF_LYC
  ldh [rSTAT], a
  ld a, IEF_LCDC|IEF_VBLANK
  ldh [rIE], a
  ldh a,[curlcdc]
  ldh [vblank_lcdc_value],a
  ldh [stat_lcdc_value],a
  ldh [rLCDC],a

.loop:
  ldh a,[bleed_help_page]
  ld b,a
  call read_pad_help_check
  jr nz,.restart

  ; Process input
  ld b,PADF_UP|PADF_DOWN|PADF_LEFT|PADF_RIGHT
  call autorepeat_60hz
  ldh a,[new_keys]
  ld b,a
  bit PADB_B,b
  ret nz

  ld a,PADF_UP|PADF_DOWN|PADF_LEFT|PADF_RIGHT
  and b
  jr z,.not_toggle
    ldh a,[curpalette]
    cpl
    ldh [curpalette],a
  .not_toggle:

  bit PADB_A,b
  jr z,.not_A
    ldh a,[curtileid]
    inc a
    cp 3
    jr c,.nowrap0
      xor a
    .nowrap0:
    ldh [curtileid],a
  .not_A:

  bit PADB_SELECT,b
  jr z,.not_select
    ldh a,[curlcdc]
    xor LCDCF_WINON
    ldh [curlcdc],a
    ldh [vblank_lcdc_value],a
    ldh [stat_lcdc_value],a
  .not_select:

  rst wait_vblank_irq
  ld a, 4
  ldh [rSCY], a
  ld a, 28
  ldh [rLYC], a
  ldh a,[curpalette]
  ld b,a
  ldh a,[curtileid]
  call load_bleed_frame_a
  call update_bcd_frame_counter
  jr .loop

; Update the frame counter
update_bcd_frame_counter:
  ld hl,CHRRAM0+$011
  push hl

  ; Write "Frame "
  ld hl,help_line_buffer
  ld de,word_frame
  call stpcpy

  ; Increment and write the frame count
  ldh a,[curframe_bcd]
  inc a
  daa
  cp $60
  jr c,.nowrap60
    xor a
  .nowrap60:
  ldh [curframe_bcd],a
  call puthex

  ; Terminate the string
  xor a
  ld [hl],a

  ; And draw it
  call vwfClearBuf

  ld hl,help_line_buffer
  ld b,0  ; X position
  call vwfPuts
  call bleed_stripwait
  pop hl
  ld c,5
  call vwfPutBufHBlank
  call bleed_stripwait
  call bleed_stripwait
  fallthrough bleed_stripwait

;;
; Skip 4 scanlines, wait for STAT IRQ, and move LYC down 28 lines
bleed_stripwait:
  ldh a, [rSCY]
  add 4
  halt
  ldh [rSCY], a
  ldh a, [rLYC]
  add 28
  ldh [rLYC], a
  ret

; If you get an error about scope, upgrade to RGBDS 0.6.2 or later,
; or a build after 2023-08-19 when #1159 was merged.
stpcpy.loop:
  inc de
  ld [hl+],a
;;
; Copies a nul-terminated string.
; @param HL destination pointer
; @param DE source string, NUL-terminated
; @return DE and HL both point to the trailing NUL
stpcpy::
  ld a,[de]
  and a
  jr nz, .loop
  ld [hl],a
  ret

; Convergence ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

activity_convergence::
  xor a
  ldh [curtileid], a   ; grid style (dot, cross, line)
  dec a
  ldh [curpalette], a  ; grid: invert grays
  ldh [curscry], a     ; bit 4: color transitions show line or not
  ; 9800: grid; 9C00: color transitions
  ld a,LCDCF_ON|LCDCF_BGON|LCDCF_BG9800|LCDCF_BG8000
  ldh [curlcdc], a

.restart:
  call clear_gbc_attr

  ; Load tiles
  ld de,convergence_chr
  ld b,sizeof_convergence_chr/16
  call pb16_unpack_to_CHRRAM0

  ; Load grid background
  ld h, $04
  call clear_scrn0_to_h

  ; TODO: Load CGB background to _SCRN1
  ldh a, [hw_capability]
  add a
  jr c, .load_plane1_gbc
  jr z, .loaded_plane1
    ; TODO: Load plane 1 for SGB
    call convergence_load_sgb_attr
    jr .loaded_plane1
  .load_plane1_gbc:
    ld hl, _SCRN1
    ld a, 1
    ldh [rVBK], a
    dec a
    ld c, 6
    .load_ctb_gbcplane1_loop:
      call convergenceB_p1_onerow
      call convergenceB_p1_onerow
      call convergenceB_p1_onerow
      xor $02
      dec c
      jr nz, .load_ctb_gbcplane1_loop
    xor a
    ldh [rVBK], a
  .loaded_plane1:

  ld hl, _SCRN1
  ld c, 6
  .load_ctb_loop:
    xor a
    call convergenceB_p0_onerow
    call convergenceB_p0_onerow
    ld a, 2
    call convergenceB_p0_onerow
    dec c
    jr nz, .load_ctb_loop

.load_new_sgb_palette:
  ldh a, [curlcdc]
  and LCDCF_BG9C00
  jr z, .sgb_palette_is_gray
    ld bc, $2A2B
    ld de, $2C2D
    call sgb_set_palettes_bcde
    jr .sgb_palette_loaded
  .sgb_palette_is_gray:
    call sgb_set_palette_gray
  .sgb_palette_loaded:

  ldh a, [curlcdc]
  ldh [vblank_lcdc_value],a
  ldh [rLCDC],a

.loop:
  ld b,helpsect_convergence
  call read_pad_help_check
  jr nz,.restart

  ldh a, [new_keys]
  ld b, a
  bit PADB_B, b
  ret nz

  ; up/down: toggle line style
  and PADF_DOWN|PADF_UP
  jr z, .not_up_down
    ldh a, [curlcdc]
    and LCDCF_BG9C00
    jr z, .up_down_on_grid
    ldh a, [curscry]
    xor $10
    ldh [curscry], a
    jr .not_up_down
  .up_down_on_grid:
    ldh a, [curtileid]
    inc a
    bit PADB_UP, b
    jr z, .only_one_inc
      inc a
    .only_one_inc:
    cp 3
    jr c, .updown_nowrap
      sub 3
    .updown_nowrap:
    ldh [curtileid], a
  .not_up_down:

  call select_inverts_palette

  ; A: switch between patterns
  ldh a, [hw_capability]
  or a
  jr z, .no_switch
  bit PADB_A, b
  jr z, .no_switch
    ldh a, [curlcdc]
    xor LCDCF_BG9C00
    ldh [curlcdc], a
    ldh [vblank_lcdc_value], a
    ldh a, [hw_capability]
    rra
    jr c, .load_new_sgb_palette
  .no_switch:

  rst wait_vblank_irq

  ; Set color palette
  ldh a, [curlcdc]
  and LCDCF_BG9C00
  jr z, .set_grid_palette
    ldh a, [curscry]
    ldh [rBGP], a  ; Show/hide lines on Super Game Boy
    and $10
    ld a, $80
    jr nz, .gbc_have_bcps
      ld a, $82
    .gbc_have_bcps:
    ld hl, convergence_gbc_palette
    ld bc, $0400 + low(rBCPS)
    ldh [c], a
    inc c
    .gbcpalloop:
      xor a
      ldh [c], a
      ldh [c], a
      ldh [c], a
      ldh [c], a
      ld a, [hl+]
      ldh [c], a
      ld a, [hl-]
      ldh [c], a
      ld a, [hl+]
      ldh [c], a
      ld a, [hl+]
      ldh [c], a
      dec b
      jr nz, .gbcpalloop
    jr .palette_done
  .set_grid_palette:
    ldh a,[curpalette]
    ld c, a
    ldh a,[curtileid]
    ld b, a
    inc b
    ld a, $FF
    .shiftloop:
      add a
      add a
      dec b
      jr nz, .shiftloop
    xor c
    call set_bgp
  .palette_done:
  jp .loop

;;
; Write 01110001110001110001
convergenceB_p1_onerow:
  ld e, $00
  jr convergenceB_draw_onerow

;;
; Write 10010010010010010010
convergenceB_p0_onerow:
  ld e, $01
  fallthrough convergenceB_draw_onerow
convergenceB_draw_onerow:
  ld d, 8
  .loop:
    xor e
    ld [hl+], a
    xor $01
    ld [hl+], a
    ld [hl+], a
    dec d
    jr nz, .loop
  ld e, 8
  add hl, de
  ret

; The data starts at lineImgBuf+6
convergence_load_sgb_attr:
  ld de, lineImgBuf
  push de
  ld hl, convergence_sgb_attrs
  call memcpy_pascal16
  ld hl, lineImgBuf+6
  ld c, 2*5
  call memcpy

  ; Make odd rows
  ld c, 3*5
  ld l, low(lineImgBuf+6)
  .oddrowloop:
    ld a, [hl+]
    xor %10101010
    ld [de], a
    inc e
    dec c
    jr nz, .oddrowloop

  ; Make other 2 rows
  ld l, low(lineImgBuf+6)
  ld c, 12*5
  call memcpy

  pop hl
  jp sgb_send

convergence_sgb_attrs:
  dw 11  ; length
  db $07*8+6  ; ATTR_CHR
  db 0  ; starting x
  db 0  ; starting y
  dw 360  ; 2-byte count
  db 0  ; row major
  db %00010101,%00000001,%01010000,%00010101,%00000001

convergence_gbc_palette:
  drgb $FF0000
  drgb $00FF00
  drgb $0000FF
  drgb $FFFFFF
